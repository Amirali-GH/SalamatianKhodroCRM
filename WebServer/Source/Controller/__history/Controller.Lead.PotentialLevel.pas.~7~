Unit Controller.Lead.PotentialLevel;

Interface

Uses
    System.SysUtils,
    System.Classes,
    System.Generics.Collections,
    MVCFramework,
    MVCFramework.Commons,
    MVCFramework.ActiveRecord,
    MVCFramework.Nullables,
    Service.Interfaces,
    Model.Lead.PotentialLevel,
    WebModule.SalamtCRM;

Type
    [MVCPath(BASE_API_V1 + '/potential-level')]
    TPotentialLevelController = Class(TMVCController)
    Public
        [MVCPath('')]
        [MVCHTTPMethods([httpGET])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure GetAllPotentialLevels(Const [MVCInject] APotentialLevelService: IPotentialLevelService);

        [MVCPath('/($APotentialLevelID)')]
        [MVCHTTPMethods([httpGET])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure GetPotentialLevelByID(Const APotentialLevelID: String;
          Const [MVCInject] APotentialLevelService: IPotentialLevelService);

        [MVCPath('')]
        [MVCHTTPMethods([httpPOST])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure CreatePotentialLevel(Const [MVCInject] APotentialLevelService: IPotentialLevelService);

        [MVCPath('/($APotentialLevelID)')]
        [MVCHTTPMethods([httpPUT])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure UpdatePotentialLevel(Const APotentialLevelID: String;
          Const [MVCInject] APotentialLevelService: IPotentialLevelService);

        [MVCPath('/($APotentialLevelID)')]
        [MVCHTTPMethods([httpDELETE])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure DeletePotentialLevel(Const APotentialLevelID: String;
          Const [MVCInject] APotentialLevelService: IPotentialLevelService);
    End;

Implementation

Uses
    MVCFramework.Serializer.Commons,
    System.JSON, FireDAC.Stan.Error;

{ TPotentialLevelController }

//________________________________________________________________________________________
Procedure TPotentialLevelController.GetAllPotentialLevels(Const APotentialLevelService: IPotentialLevelService);
Var
    LPotentialLevelList: TObjectList<TLead_PotentialLevel>;
    LEqualIndex: Integer;
    LPageArrayData: TArray<string>;
    LCurrPage, LPageData, Key, Value: String;
    LMetaJSON, LPageJSON: TJSONObject;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        Try
            LCurrPage := Context.Request.Params['page'];
            LPotentialLevelList := APotentialLevelService.GetAllPotentialLevels(LCurrPage, True);
            If Assigned(LPotentialLevelList) then
            Begin
                LPageJSON := TJSONObject.Create;
                Try
                    LPageArrayData := GetPaginationData(lCurrPage.ToInteger,
                                                        LPotentialLevelList.Count,
                                                        PAGE_SIZE,
                                                        BASE_API_V1 + '/potential-level?page=($page)')
                                                      .ToString.Split([';']);
                    For LPageData in LPageArrayData do
                    Begin
                        LEqualIndex := LPageData.IndexOf('=');
                        If (LEqualIndex > 0) then
                        Begin
                            Key := LPageData.Substring(0, LEqualIndex).Trim;
                            Value := LPageData.Substring(LEqualIndex + 1).Trim;
                            LPageJSON.AddPair(Key, Value);
                        End;
                    End;

                    LMetaJSON.AddPair('data_type', 'list<model_lead_potential_level>');
                    LMetaJSON.AddPair('count', LPotentialLevelList.Count);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('description', 'لیست تمام سطوح پتانسیل ثبت شده');

                    Render(HTTP_STATUS.OK,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LPotentialLevelList,
                              Procedure(Const Obj: TObject; Const Links: IMVCLinks)
                              Begin
                                  Links.AddRefLink
                                        .Add(HATEOAS._TYPE, TMVCMediaType.APPLICATION_JSON)
                                        .Add(HATEOAS.HREF, Format(BASE_API_V1 + '/potential-level/%d', [TLead_PotentialLevel(Obj).PotentialLevelID]))
                                        .Add(HATEOAS.REL, 'self');
                              End)
                    );
                Finally
                    LPotentialLevelList.Free;
                End;
            End
            Else
            Begin
                Raise Exception.Create('هنگام خواندن سطوح پتانسیل ثبت شده خطایی رخ داده است!');
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'list<model_lead_potential_level>');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(HTTP_STATUS.InternalServerError,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TList.Create)
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TPotentialLevelController.GetPotentialLevelByID(Const APotentialLevelID: String;
  Const APotentialLevelService: IPotentialLevelService);
Var
    LStatusCode, LPotentialLevelID: Integer;
    LPotentialLevel: TLead_PotentialLevel;
    LMetaJSON: TJSONObject;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            If (APotentialLevelID.IsEmpty) OR (Not TryStrToInt(APotentialLevelID, LPotentialLevelID)) Then
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('شناسه سطح پتانسیل نامعتبر است!');
            End;

            LPotentialLevel := APotentialLevelService.GetPotentialLevelByID(LPotentialLevelID);
            If Assigned(LPotentialLevel) Then
            Begin
                Try
                    LStatusCode := HTTP_STATUS.OK;

                    LMetaJSON.AddPair('data_type', 'model_lead_potential_level');
                    LMetaJSON.AddPair('count', 1);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('description', Format('سطح پتانسیل با کد %s یافت شد.', [LPotentialLevel.Code]));

                    Render(LStatusCode,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LPotentialLevel)
                    );
                Finally
                    LPotentialLevel.Free;
                End;
            End
            Else
            Begin
                LStatusCode := HTTP_STATUS.Forbidden;
                Raise EMVCException.Create('سطح پتانسیل یافت نشد');
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'model_lead_potential_level');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TPotentialLevelController.CreatePotentialLevel(Const APotentialLevelService: IPotentialLevelService);
Var
    LPotentialLevelInput: TLead_PotentialLevel;
    LCreated: TLead_PotentialLevel;
    LMetaJSON: TJSONObject;
    LStatusCode: Integer;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            LPotentialLevelInput := Context.Request.BodyAs<TLead_PotentialLevel>;
            If Not Assigned(LPotentialLevelInput) Then
            Begin
                LStatusCode := HTTP_STATUS.BadRequest;
                Raise EMVCException.Create('داده ورودی نامعتبر است');
            End;

            Try
                LCreated := Nil;
                Try
                    LCreated := APotentialLevelService.CreatePotentialLevel(LPotentialLevelInput);
                Except
                    On E: EFDException do
                    Begin
                        If Assigned(LCreated) then
                        Begin
                            LCreated.Free;
                        End;

                        If Pos('duplicate', E.Message.ToLower) > 0 then
                        Begin
                            Raise EMVCException.Create('کد سطح پتانسیل تکراری است');
                        End
                        Else
                        Begin
                            Raise EMVCException.Create('خطای پایگاه داده: ' + E.Message);
                        End;
                    End;

                    On E: Exception do
                    Begin
                        Raise EMVCException.Create(E.Message);
                    End;
                End;

                Try
                    LStatusCode := HTTP_STATUS.Created;

                    LMetaJSON.AddPair('data_type', 'model_lead_potential_level');
                    LMetaJSON.AddPair('count', 1);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('url', BASE_API_V1 + '/potential-level/' + LCreated.PotentialLevelID.ToString);
                    LMetaJSON.AddPair('description', 'سطح پتانسیل با موفقیت ذخیره شد.');

                    Render(LStatusCode,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LCreated)
                    );
                Finally
                    If Assigned(LCreated) then
                    Begin
                        LCreated.Free;
                    End;
                End;
            Finally
                LPotentialLevelInput.Free;
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'model_lead_potential_level');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TPotentialLevelController.UpdatePotentialLevel(Const APotentialLevelID: String;
  Const APotentialLevelService: IPotentialLevelService);
Var
    LPotentialLevelID: Integer;
    LPotentialLevelInput: TLead_PotentialLevel;
    LUpdated: TLead_PotentialLevel;
    LMetaJSON: TJSONObject;
    LStatusCode: Integer;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            If (APotentialLevelID.IsEmpty) OR (Not TryStrToInt(APotentialLevelID, LPotentialLevelID)) Then
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('شناسه سطح پتانسیل نامعتبر است!');
            End;

            LPotentialLevelInput := Context.Request.BodyAs<TLead_PotentialLevel>;
            If Not Assigned(LPotentialLevelInput) Then
            Begin
                LStatusCode := HTTP_STATUS.BadRequest;
                Raise EMVCException.Create('داده ورودی نامعتبر است');
            End;

            Try
                LUpdated := APotentialLevelService.UpdatePotentialLevelPartial(LPotentialLevelID, LPotentialLevelInput);
                If Not Assigned(LUpdated) Then
                Begin
                    LStatusCode := HTTP_STATUS.NotFound;
                    Raise EMVCException.Create('سطح پتانسیل یافت نشد');
                End;

                Try
                    LStatusCode := HTTP_STATUS.OK;

                    LMetaJSON.AddPair('data_type', 'model_lead_potential_level');
                    LMetaJSON.AddPair('count', 1);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('description', 'سطح پتانسیل با موفقیت بروزرسانی شد.');

                    Render(LStatusCode,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LUpdated)
                    );
                Finally
                    LUpdated.Free;
                End;
            Finally
                LPotentialLevelInput.Free;
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'model_lead_potential_level');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TPotentialLevelController.DeletePotentialLevel(Const APotentialLevelID: String;
  Const APotentialLevelService: IPotentialLevelService);
Var
    LStatusCode, LPotentialLevelID: Integer;
    LMetaJSON: TJSONObject;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            If (APotentialLevelID.IsEmpty) OR (Not TryStrToInt(APotentialLevelID, LPotentialLevelID)) Then
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('شناسه سطح پتانسیل نامعتبر است!');
            End;

            If APotentialLevelService.DeletePotentialLevel(LPotentialLevelID) Then
            Begin
                LStatusCode := HTTP_STATUS.OK;

                LMetaJSON.AddPair('data_type', 'integer');
                LMetaJSON.AddPair('count', 1);
                LMetaJSON.AddPair('is_success', True);
                LMetaJSON.AddPair('description', 'سطح پتانسیل با موفقیت حذف شد.');

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', StrToJSONObject(TJSONObject.Create(TJSONPair.Create('potentiallevelid', LPotentialLevelID)).ToString))
                );
            End
            Else
            Begin
                Raise EMVCException.Create('سطح پتانسیل مورد نظر یافت نشد!');
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'integer');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________

End.
