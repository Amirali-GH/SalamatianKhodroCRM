Unit Controller.Organization;

Interface

Uses
    System.SysUtils,
    System.Classes,
    System.Generics.Collections,
    MVCFramework,
    MVCFramework.Commons,
    MVCFramework.ActiveRecord,
    MVCFramework.Swagger.Commons,
    MVCFramework.Nullables,
    Service.Interfaces,
    Model.Organization.Employee,
    WebModule.SalamtCRM;

Type
    [MVCPath(BASE_API_V1 + '/employee')]
    TOrganizationEmployeeController = Class(TMVCController)
    Public
        [MVCPath('')]
        [MVCHTTPMethods([httpGET])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure GetAllEmployees(Const [MVCInject] AEmployeeService: IEmployeeService);

        [MVCPath('/($AEmployeeID)')]
        [MVCHTTPMethods([httpGET])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure GetEmployeeByID(Const AEmployeeID: String;
          Const [MVCInject] AEmployeeService: IEmployeeService);

        [MVCPath('')]
        [MVCHTTPMethods([httpPOST])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure CreateEmployee(Const [MVCInject] AEmployeeService: IEmployeeService);

        [MVCPath('/($AEmployeeID)')]
        [MVCHTTPMethods([httpPUT])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure UpdateEmployee(Const AEmployeeID: String;
          Const [MVCInject] AEmployeeService: IEmployeeService);

        [MVCPath('/($AEmployeeID)')]
        [MVCHTTPMethods([httpDELETE])]
        [MVCProduces(TMVCMediaType.APPLICATION_JSON)]
        [MVCConsumes(TMVCMediaType.APPLICATION_JSON)]
        Procedure DeleteEmployee(Const AEmployeeID: String;
          Const [MVCInject] AEmployeeService: IEmployeeService);
    End;

Implementation

Uses
    MVCFramework.Serializer.Commons,
    System.JSON, FireDAC.Stan.Error;

{ TOrganizationEmployeeController }

//________________________________________________________________________________________
Procedure TOrganizationEmployeeController.GetAllEmployees(Const AEmployeeService: IEmployeeService);
Var
    LEmployeeList: TObjectList<TOrganization_Employee>;
    LEqualIndex: Integer;
    LPageArrayData: TArray<string>;
    LCurrPage, LPageData, Key, Value, LStatus, LContext: String;
    LMetaJSON, LPageJSON: TJSONObject;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        Try
            LCurrPage := Context.Request.Params['page'];
            LStatus := Context.Request.Params['status'];
            LContext := Context.Request.Params['context'];
            LEmployeeList := AEmployeeService.GetAllEmployees(LCurrPage, LStatus, LContext);
            If Assigned(LEmployeeList) then
            Begin
                LPageJSON := TJSONObject.Create;
                Try
                    LPageArrayData := GetPaginationData(LCurrPage.ToInteger,
                                                        LEmployeeList.Count,
                                                        PAGE_SIZE,
                                                        BASE_API_V1 + '/employee?page=($page)')
                                                      .ToString.Split([';']);
                    For LPageData in LPageArrayData do
                    Begin
                        LEqualIndex := LPageData.IndexOf('=');
                        If (LEqualIndex > 0) then
                        Begin
                            Key := LPageData.Substring(0, LEqualIndex).Trim;
                            Value := LPageData.Substring(LEqualIndex + 1).Trim;
                            LPageJSON.AddPair(Key, Value);
                        End;
                    End;

                    LMetaJSON.AddPair('page', LPageJSON);
                    LMetaJSON.AddPair('data_type', 'list<model_organization_employee>');
                    LMetaJSON.AddPair('count', LEmployeeList.Count);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('description', '·?”   „«„ ò«—„‰œ«‰ À» ù‘œÂ');

                    Render(HTTP_STATUS.OK,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LEmployeeList,
                              Procedure(Const Obj: TObject; Const Links: IMVCLinks)
                              Begin
                                  Links.AddRefLink
                                        .Add(HATEOAS._TYPE, TMVCMediaType.APPLICATION_JSON)
                                        .Add(HATEOAS.HREF, Format(BASE_API_V1 + '/employee/%d', [TOrganization_Employee(Obj).EmployeeID]))
                                        .Add(HATEOAS.REL, 'self');
                              End)
                    );
                Finally
                    LEmployeeList.Free;
                End;
            End
            Else
            Begin
                Raise Exception.Create('Â‰ê«„ ŒÊ«‰œ‰ ò«—„‰œ«‰ À» ù‘œÂ Œÿ«?? —Œ œ«œÂ «” !');
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'list<model_organization_employee>');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(HTTP_STATUS.InternalServerError,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TList.Create)
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TOrganizationEmployeeController.GetEmployeeByID(Const AEmployeeID: String;
  Const AEmployeeService: IEmployeeService);
Var
    LStatusCode, LEmployeeID: Integer;
    LEmployee: TOrganization_Employee;
    LMetaJSON: TJSONObject;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            If (AEmployeeID.IsEmpty) OR (Not TryStrToInt(AEmployeeID, LEmployeeID)) Then
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('‘‰«”Â ò«—„‰œ ‰«„⁄ »— «” !');
            End;

            LEmployee := AEmployeeService.GetEmployeeByID(LEmployeeID);
            If Assigned(LEmployee) Then
            Begin
                Try
                    LStatusCode := HTTP_STATUS.OK;

                    LMetaJSON.AddPair('data_type', 'model_organization_employee');
                    LMetaJSON.AddPair('count', 1);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('description', Format('ò«—„‰œ »« òœ „·? %s ?«›  ‘œ.', [LEmployee.NationalCode.ValueOrDefault]));

                    Render(LStatusCode,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LEmployee)
                    );
                Finally
                    LEmployee.Free;
                End;
            End
            Else
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('ò«—„‰œ ?«›  ‰‘œ');
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'model_organization_employee');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TOrganizationEmployeeController.CreateEmployee(Const AEmployeeService: IEmployeeService);
Var
    LEmployeeInput: TOrganization_Employee;
    LCreated: TOrganization_Employee;
    LMetaJSON: TJSONObject;
    LStatusCode: Integer;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            LEmployeeInput := Context.Request.BodyAs<TOrganization_Employee>;
            If Not Assigned(LEmployeeInput) Then
            Begin
                LStatusCode := HTTP_STATUS.BadRequest;
                Raise EMVCException.Create('œ«œÂ Ê—Êœ? ‰«„⁄ »— «” ');
            End;

            Try
                LCreated := Nil;
                Try
                    LCreated := AEmployeeService.CreateEmployee(LEmployeeInput);
                Except
                    On E: EFDException do
                    Begin
                        If Assigned(LCreated) then
                        Begin
                            LCreated.Free;
                        End;

                        If Pos('duplicate', E.Message.ToLower) > 0 then
                        Begin
                            LStatusCode := HTTP_STATUS.Conflict;
                            Raise EMVCException.Create('òœ „·? ?« «?„?· ò«—„‰œ  ò—«—? «” ');
                        End
                        Else
                        Begin
                            Raise EMVCException.Create('Œÿ«? Å«?ê«Â œ«œÂ: ' + E.Message);
                        End;
                    End;

                    On E: Exception do
                    Begin
                        Raise EMVCException.Create(E.Message);
                    End;
                End;
                Try
                    LStatusCode := HTTP_STATUS.Created;

                    LMetaJSON.AddPair('data_type', 'model_organization_employee');
                    LMetaJSON.AddPair('count', 1);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('url', BASE_API_V1 + '/employee/' + LCreated.EmployeeID.ToString);
                    LMetaJSON.AddPair('description', 'ò«—„‰œ »« „Ê›ﬁ?  –Œ?—Â ‘œ.');

                    Render(LStatusCode,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LCreated)
                    );
                Finally
                    LCreated.Free;
                End;
            Finally
                LEmployeeInput.Free;
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'model_organization_employee');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TOrganizationEmployeeController.UpdateEmployee(Const AEmployeeID: String;
  Const AEmployeeService: IEmployeeService);
Var
    LEmployeeID: Integer;
    LEmployeeInput: TOrganization_Employee;
    LUpdated: TOrganization_Employee;
    LMetaJSON: TJSONObject;
    LStatusCode: Integer;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            If (AEmployeeID.IsEmpty) OR (Not TryStrToInt(AEmployeeID, LEmployeeID)) Then
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('‘‰«”Â ò«—„‰œ ‰«„⁄ »— «” !');
            End;

            LEmployeeInput := Context.Request.BodyAs<TOrganization_Employee>;
            If Not Assigned(LEmployeeInput) Then
            Begin
                LStatusCode := HTTP_STATUS.BadRequest;
                Raise EMVCException.Create('œ«œÂ Ê—Êœ? ‰«„⁄ »— «” ');
            End;

            Try
                Try
                    LUpdated := AEmployeeService.UpdateEmployeePartial(LEmployeeID, LEmployeeInput);
                Except
                    On E: EFDException do
                    Begin
                        If Pos('duplicate', E.Message.ToLower) > 0 then
                        Begin
                            LStatusCode := HTTP_STATUS.Conflict;
                            Raise EMVCException.Create('òœ „·? ?« «?„?· ò«—„‰œ  ò—«—? «” ');
                        End
                        Else
                        Begin
                            Raise EMVCException.Create('Œÿ«? Å«?ê«Â œ«œÂ: ' + E.Message);
                        End;
                    End;

                    On E: Exception do
                    Begin
                        Raise EMVCException.Create(E.Message);
                    End;
                End;

                If Not Assigned(LUpdated) Then
                Begin
                    LStatusCode := HTTP_STATUS.NotFound;
                    Raise EMVCException.Create('ò«—„‰œ ?«›  ‰‘œ');
                End;

                Try
                    LStatusCode := HTTP_STATUS.OK;

                    LMetaJSON.AddPair('data_type', 'model_organization_employee');
                    LMetaJSON.AddPair('count', 1);
                    LMetaJSON.AddPair('is_success', True);
                    LMetaJSON.AddPair('description', 'ò«—„‰œ »« „Ê›ﬁ?  »Âù—Ê“—”«‰? ‘œ.');

                    Render(LStatusCode,
                        ObjectDict(False)
                          .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                          .Add('data', LUpdated)
                    );
                Finally
                    LUpdated.Free;
                End;
            Finally
                LEmployeeInput.Free;
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'model_organization_employee');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________
Procedure TOrganizationEmployeeController.DeleteEmployee(Const AEmployeeID: String;
  Const AEmployeeService: IEmployeeService);
Var
    LStatusCode, LEmployeeID: Integer;
    LMetaJSON: TJSONObject;
Begin
    LMetaJSON := TJSONObject.Create;
    Try
        LStatusCode := HTTP_STATUS.InternalServerError;
        Try
            If (AEmployeeID.IsEmpty) OR (Not TryStrToInt(AEmployeeID, LEmployeeID)) Then
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('‘‰«”Â ò«—„‰œ ‰«„⁄ »— «” !');
            End;

            If AEmployeeService.DeleteEmployee(LEmployeeID) Then
            Begin
                LStatusCode := HTTP_STATUS.OK;

                LMetaJSON.AddPair('data_type', 'integer');
                LMetaJSON.AddPair('count', 1);
                LMetaJSON.AddPair('is_success', True);
                LMetaJSON.AddPair('description', 'ò«—„‰œ »« „Ê›ﬁ?  Õ–› ‘œ.');

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', StrToJSONObject(TJSONObject.Create(TJSONPair.Create('employeeid', LEmployeeID)).ToString))
                );
            End
            Else
            Begin
                LStatusCode := HTTP_STATUS.NotFound;
                Raise EMVCException.Create('ò«—„‰œ „Ê—œ ‰Ÿ— ?«›  ‰‘œ!');
            End;
        Except
            On E: Exception do
            Begin
                LMetaJSON.AddPair('data_type', 'integer');
                LMetaJSON.AddPair('count', 0);
                LMetaJSON.AddPair('is_success', False);
                LMetaJSON.AddPair('description', E.Message);

                Render(LStatusCode,
                    ObjectDict(True)
                      .Add('meta', StrToJSONObject(LMetaJSON.ToString))
                      .Add('data', TMVCObjectDictionary.Create())
                );
            End;
        End;
    Finally
        LMetaJSON.Free;
    End;
End;
//________________________________________________________________________________________

End.
